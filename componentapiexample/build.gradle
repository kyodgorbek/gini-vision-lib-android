apply plugin: 'com.android.application'
apply from: rootProject.file('gradle/git_utils.gradle')
apply from: rootProject.file('gradle/gini_credentials.gradle')

def appVersionCode = gitCommitUnixTime()
def appVersionName = "${version}-${gitBranch()}-${gitHash()} (${appVersionCode})"

task printVersion {
    doLast {
        println "${appVersionName}"
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "net.gini.android.vision.componentapiexample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode appVersionCode
        versionName appVersionName
    }

    signingConfigs {
        release {
            storeFile file(releaseKeystoreFile)
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
    }

    buildTypes {
        debug {
            resValue 'string', 'gini_api_client_id', clientId
            resValue 'string', 'gini_api_client_secret', clientSecret
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue 'string', 'gini_api_client_id', clientId
            resValue 'string', 'gini_api_client_secret', clientSecret
        }
    }

    sourceSets {
        main {
            java.srcDirs += "$rootProject.projectDir/exampleShared/java"
        }
    }
}

// For testing a released version
repositories {
    maven {
        url 'https://repo.gini.net/nexus/content/repositories/open'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile deps.appCompatv7

    compile 'com.karumi:dexter:4.2.0'

    compile 'com.github.tony19:logback-android-core:1.1.1-5'
    compile 'com.github.tony19:logback-android-classic:1.1.1-5'

    compile('net.gini:gini-android-sdk:1.4.1@aar') {
        transitive = true
    }

    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile deps.junit

    // For testing the local version
    compile project(path: ':ginivision')
    // For testing a released version
//    compile 'net.gini:gini-vision-lib:2.0.0-alpha.1'
}

apply from: rootProject.file('gradle/codequality.gradle')
